---
template: overrides/blogs.html
tags:
  - knowledge graph
---

# Neo4J Graph Database Anti-Fraud Analysis Practice (Part 4) - Risk Scoring

!!! info
    Author: [Vincent](https://github.com/Realvincentyuan), published on 2021-06-06, Reading Time: about 6 minutes, WeChat Official Account Article Link: [:fontawesome-solid-link:](https://mp.weixin.qq.com/s?__biz=MzI4Mjk3NzgxOQ==&mid=2247485268&idx=1&sn=2f32b4911dd64a5696f2ef287b9542a0&chksm=eb90f420dce77d36489cd350674af9c4523c05d884afae5a158c619faaabfca5e161b9443131&token=200682583&lang=zh_CN#rd)

## 1 Preface

In the Neo4J graph database anti-fraud series, we have identified risky users. This article will explain how to score the risk of each customer. If you need to review the previous articles, you can directly jump to the links:

- [Introduction to Graph Database](https://mp.weixin.qq.com/s?__biz=MzI4Mjk3NzgxOQ==&mid=2247485112&idx=1&sn=efd4f9b472a3d58378407bb6fad46a2f&chksm=eb90f5ccdce77cda0285d53331834a787364d4458a3588173c9fe8ef6953499362bd64f7c918&token=1650861834&lang=zh_CN#rd)
- [Neo4j Graph Database Anti-Fraud Analysis Practice (Part 1) - Build Analysis Environment](https://mp.weixin.qq.com/s?__biz=MzI4Mjk3NzgxOQ==&mid=2247485213&idx=1&sn=ed9051c887847bcf35ef6e21af6005f4&chksm=eb90f469dce77d7fa1325810d537e183c65eef7067f20219eee02d6f3667d14554832103986d&token=771475859&lang=zh_CN#rd)
- [Neo4j Graph Database Anti-Fraud Analysis Practice (Part 2) - Data Preparation](https://mp.weixin.qq.com/s?__biz=MzI4Mjk3NzgxOQ==&mid=2247485256&idx=1&sn=0d87a1d090f7282f85f3d2395372c8ed&chksm=eb90f43cdce77d2af75e6313e945a83f2309743e7e7e0855c99d9ecece6f3d24f3ad06ae80a4&token=771475859&lang=zh_CN#rd)
- [Neo4j Graph Database Anti-Fraud Analysis Practice (Part 3) - Identify Criminal Organizations](ttps://mp.weixin.qq.com/s?__biz=MzI4Mjk3NzgxOQ==&mid=2247485261&idx=1&sn=9bbe4e099d7e199d749540797e82e443&chksm=eb90f439dce77d2fb5a8f06707844f0eef22667821f97d9e509563a36cebb2ed1903994056dd&token=1481538225&lang=zh_CN#rd)

## 2 Finding Similar Nodes

Last time, we used the [Weakly Connected Components](https://neo4j.com/docs/graph-data-science/current/algorithms/wcc/ 'Weakly Connected Components') algorithm to cluster the data. Next, we can look for similar customers in each cluster.

<figure>
  <img src="https://cdn.jsdelivr.net/gh/BulletTech2021/Pics/img/1_V/cluster_1.png"  />
  <figcaption>WCC clustering results</figcaption>
</figure>

This step uses node similarity ([Jaccard coefficient](https://neo4j.com/docs/graph-data-science/current/algorithms/node-similarity/#algorithms-node-similarity 'Node similarity')) to judge. The node similarity algorithm requires two types of nodes in the graph structure, so we create the `Similarity` graph, which contains two types of nodes: `Client` and `Entity` information nodes, and their corresponding relationships.

```sql
CALL gds.graph.project.cypher('Similarity',
'MATCH(c:Client)
    WHERE exists(c.firstPartyFraudGroup)
    RETURN id(c) AS id,labels(c) AS labels
UNION
MATCH(n)
    WHERE n:Email OR n:Phone OR n:SSN
    RETURN id(n) AS id,labels(n) AS labels',
'MATCH(c:Client)
-[:HAS_EMAIL|:HAS_PHONE|:HAS_SSN]->(ids)
WHERE exists(c.firstPartyFraudGroup)
RETURN id(c) AS source,id(ids) AS target')
YIELD graphName,nodeCount,relationshipCount;
```

After creating the graph, we can run the node similarity algorithm (note: the algorithm supports weighting, see the documentation for details）：

```sql
CALL gds.nodeSimilarity.stream('Similarity',{topK:15})
YIELD node1,node2,similarity
RETURN gds.util.asNode(node1).id AS client1,
    gds.util.asNode(node2).id AS client2,similarity
ORDER BY similarity;
```

Use the `Mutate` mode to write the results to the graph in memory, creating a new relationship `SIMILAR_TO`. At this point, the similarity between the pairs of similar nodes can be measured using the jaccardScore:

```SQL
CALL gds.nodeSimilarity.mutate('Similarity',{topK:15,
  mutateProperty:'jaccardScore', mutateRelationshipType:'SIMILAR_TO'});

-- Write the results from the in-memory graph to the database
CALL gds.graph.writeRelationship('Similarity','SIMILAR_TO','jaccardScore');
```

## 3 Creating Risk Scores

Next, we use the [Node Centrality algorithm](https://neo4j.com/docs/graph-data-science/current/algorithms/degree-centrality/ 'Degree Centrality') combined with the similarity indicator generated above to generate a `firstPartyFraudScore`. The higher the `firstPartyFraudScore`, the more similar the entity information of the client with many customers in a cluster, indicating that it is more dangerous. 

```SQL
CALL gds.degree.write('Similarity',{nodeLabels:['Client'],
    relationshipTypes:['SIMILAR_TO'],
    relationshipWeightProperty:'jaccardScore',
    writeProperty:'firstPartyFraudScore'});
```

<figure>
  <img src="https://cdn.jsdelivr.net/gh/BulletTech2021/Pics/img/1_V/Score.png"  />
  <figcaption>Risk score distribution</figcaption>
</figure>

Finally, we select the 80th percentile as the threshold to mark dangerous clients. In actual applications, you can also use `firstPartyFraudScore` as a separate feature and integrate it into an anti-fraud strategy or model.

```sql
MATCH(c:Client)
WHERE exists(c.firstPartyFraudScore)
WITH percentileCont(c.firstPartyFraudScore, 0.8)
    AS firstPartyFraudThreshold
MATCH(c:Client)
WHERE c.firstPartyFraudScore>firstPartyFraudThreshold
SET c:FirstPartyFraudster;
```

## 4 Summary

The Neo4J Graph Database Anti-Fraud Analysis Practical Series has come to an end. Through this series of articles, we have learned and consolidated the following knowledge points:

- Basics of graph structure
- Neo4J graph database query language Cypher common commands
- Common algorithms in anti-fraud applications

Hope that this practical series of sharing will be helpful to you. Feel free to leave a message in the comment area for discussion!

<figure>
  <img src="https://cdn.jsdelivr.net/gh/BulletTech2021/Pics/2021-6-14/1623639526512-1080P%20(Full%20HD)%20-%20Tail%20Pic.png" width="500" />
</figure>